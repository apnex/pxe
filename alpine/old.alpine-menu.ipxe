#!ipxe

# set variables
set hostname alpine
set apkovl http://pxe.apnex.io/alpine/static-apkvol.tar.gz
set alpine-repo http://dl-cdn.alpinelinux.org/alpine
set version latest-stable
set arch x86_64
set flavor virt
set netboot ${alpine-repo}/${version}/releases/${arch}/netboot

:start
menu
item dhcp	alpine [dhcp]
item static	alpine [static]
item shell	shell
choose --default dhcp --timeout 10000 target && goto ${target}

:dhcp
ifopen net0
dhcp
echo ADDRESS-: ${net0/ip}
echo NETMASK-: ${net0/netmask}
echo GATEWAY-: ${net0/gateway}
echo DNS-----: ${dns}

#kernel ${mirror}/images/pxeboot/vmlinuz initrd=initrd.img ks=${kickstart} ksdevice=eth0 net.ifnames=0 biosdevname=0 ip=::::centos:eth0:dhcp
initrd ${netboot}/initramfs-${flavor}
kernel ${netboot}/vmlinuz-${flavor} \
    ip=dhcp \
    console=ttyS0,19200 \
    modules=loop,squashfs nomodeset \
    modloop=${netboot}/modloop-${flavor} \
    alpine_repo=${alpine-repo}/${version}/main/
boot

:static
ifopen net0
echo -n net0.address-: && read net0/ip
echo -n net0.netmask-: && read net0/netmask
echo -n net0.gateway-: && read net0/gateway
echo -n net0.dns-----: && read dns
echo ADDRESS-: ${net0/ip}
echo NETMASK-: ${net0/netmask}
echo GATEWAY-: ${net0/gateway}
echo DNS-----: ${dns}

#kernel ${mirror}/images/pxeboot/vmlinuz initrd=initrd.img ks=${kickstart} ksdevice=eth0 net.ifnames=0 biosdevname=0 ip=${net0/ip}::${net0/gateway}:${net0/netmask}:centos:eth0:none nameserver=${dns}
#kernel params for ip/dns
#https://www.kernel.org/doc/Documentation/filesystems/nfs/nfsroot.txt

route
ifstat net0

initrd ${netboot}/initramfs-${flavor}
kernel ${netboot}/vmlinuz-${flavor} \
    console=ttyS0,19200 \
    ip=${net0/ip}::${net0/gateway}:${net0/netmask}:::none:${dns} \
    modules=loop,squashfs nomodeset \
    modloop=${netboot}/modloop-${flavor} \
    alpine_repo=${alpine-repo}/${version}/main/ \
    apkovl=${apkovl}
boot

:shell
echo Type 'exit' to get the back to the menu
shell
goto start
